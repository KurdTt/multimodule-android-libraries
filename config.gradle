ext.packageName = "com.pk.example.multimodule"

ext.javaConfig = {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

ext.projectDefaultConfig = {
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName getVersion()
    }
}

ext.buildTypesConfig = {
    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled false
        }
        release {
            minifyEnabled true
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

ext.mavenPublisher = {
    task androidSourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }

    afterEvaluate {
        publishing {
            publications {
                maven(MavenPublication) {
                    from components.debug
                    artifact androidSourcesJar
                    groupId = packageName
                    artifactId = findProperty('moduleName')
                    version = getVersion()
                }
            }
        }
    }
}